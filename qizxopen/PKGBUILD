# Contributor: Vojtech Horky <vojta . horky at-symbol seznam . cz>
pkgname=qizxopen
pkgver=4.0
pkgrel=1
pkgdesc="XML indexing and searching engine, fully supporting XQuery"
url="http://www.xmlmind.com/qizx/qizxopen.shtml"
arch=('i686' 'x86_64')
license=('MPL')
depends=('java-runtime' 'apache-ant' 'bash')
source=("http://www.xmlmind.net/qizx/_download/${pkgname}-${pkgver}.zip")
md5sums=('942beb23e536580aacb536d68a6578b0')

build() {
	# As the ZIP package does not contain any installer, the files
	# are copied manually
	mkdir -p ${pkgdir}/usr/share/java/${pkgname} || return 1;
	# copy the libraries
	cp ${srcdir}/${pkgname}-${pkgver}/lib/*.jar ${pkgdir}/usr/share/java/${pkgname}
	# edit the launcher to use correct paths
	msg "Customizing launcher scripts...";
	mkdir -p ${pkgdir}/usr/bin
	for f in qizx qizxstudio; do
		sed -e 's#dlib=$distrib/lib#'"dlib=/usr/share/java/${pkgname}"'#' \
			<${srcdir}/${pkgname}-${pkgver}/bin/$f \
			>${pkgdir}/usr/bin/$f;
		chmod +x ${pkgdir}/usr/bin/$f;
	done
	# copy the documentation
	mkdir -p ${pkgdir}/usr/share/doc/${pkgname} || return 1;
	cp -R ${srcdir}/${pkgname}-${pkgver}/docs \
		${srcdir}/${pkgname}-${pkgver}/README.html \
		${pkgdir}/usr/share/doc/${pkgname}
	find ${pkgdir}/usr/share/doc/${pkgname} -type f -exec chmod 0644 {} \;	
}

